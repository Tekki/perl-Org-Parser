#!/usr/bin/perl

# a simple example script to dump org structure

# workaround for dzil
package dump_org_structure;

use 5.010;
use strict;
use warnings;
use Log::Any qw($log);

use String::Escape qw(elide printable);
use Org::Parser;

my $doc = Org::Parser->new->parse([<>]);
dump_element($doc);

sub dump_element {
    my ($el, $indent_level) = @_;
    $indent_level //= 0;

    my $line = "  " x $indent_level;
    my $type = ref($el);
    $type =~ s/^Org::(?:Element::)?//;
    $line .= "$type:";
    # per-element important info
    if ($type eq 'Headline') {
        $line .= " l=".$el->level;
        $line .= ",todo=".$el->todo_state if $el->todo_state;
    } elsif ($type eq 'List') {
        $line .= " ".$el->type;
        $line .= " ".$el->bullet_style;
        $line .= " indent=".length($el->indent);
    } elsif ($type eq 'ListItem') {
        $line .= " ".$el->bullet;
    } elsif ($type eq 'Text') {
        $line .= " mu_start" if $el->{_mu_start}; #TMP
        $line .= " mu_end" if $el->{_mu_end}; #TMP
        $line .= " ".$el->style if $el->style;
    } elsif ($type eq 'Timestamp') {
        $line .= " A" if $el->is_active;
        $line .= " dt=".$el->datetime;
    } elsif ($type eq 'TimeRange') {
        $line .= " A" if $el->is_active;
        $line .= " dt1=".$el->datetime1;
        $line .= " dt2=".$el->datetime2;
    }
    unless ($el->children) {
        $line .= " \"".
            printable elide($el->_str // $el->as_string, 80-length($line)-2);
        $line .= "\"";
    }
    say $line;

    if ($type eq 'Headline') {
        print "  " x ($indent_level+1), "(title)\n";
        dump_element($el->title, $indent_level+1);
        print "  " x ($indent_level+1), "(children)\n" if $el->children;
    }

    if ($el->children) {
        dump_element($_, $indent_level+1) for @{ $el->children };
    }
}
